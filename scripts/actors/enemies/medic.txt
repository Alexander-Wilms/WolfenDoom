/*
 * Copyright (c) 2017-2020 AFADoomer
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
**/

/*

  An actor that looks for dead 'Nazi' class actors to be healed.

  Automatically searches for the closest visible dead 'Nazi' actor that it is allied with (that
  has the same FRIENDLY flag settings) that is not gibbed or burned to ash.  Once the actor is
  identified, the Medic sets the actor as its goal and runs to it.

  Once within range of the goal actor (24 units centerpoint to centerpoint), the Medic enters its
  "Heal" state and begins to resurrect the dead actor.

  While the dead actor is being healed, "HealingParticle" actors are spawned from the dead actor.
  The number of particles spawned, and the number of repetitions of the "Heal" state animation that
  are played, is based on the spawn health of the dead actor; one iteration of the heal animation
  (5 particle spawns) for every 25 hitpoints, with a maximum of 3 iterations.

  The now ressurected actor's target is cleared, and, if they are sneakable, they are set idle.

  The Medic also tries to avoid the player, by running away if a player is visible within 192
  units.  When this happens, the medic also forgets what dead body it was going toward.

  Also can work as FRIENDLY, healing only the FRIENDLY dead actors.

*/
class NaziMedic : NaziStandard
{
	bool crouched;

	Default
	{
		//$Category Monsters (BoA)/Others
		//$Title Nazi Medic
		//$Color 4
		Health 20;
		Speed 4;
		+AVOIDMELEE
		+NEVERTARGET
		Base.NoMedicHeal;
		Nazi.NoRandomDeaths 2; //no backfall death
		Nazi.Healer HLR_ALLIES;
		Nazi.HealSound "misc/health_pkup";
		DropItem "NaziMedicBox", 255;
	}

	States
	{
		Spawn:
			NMDC N 1; // Just sets the base sprite
			Goto Look;
		Look:
			"####" "#" 35 A_LookForBodies();
			"####" "#" 0 A_CheckForPlayer(True);
			Loop;
		See:
			Goto See.Dodge;
		Dodge:
			NMDD A 0;
		Dodge.Resume:
			Goto See.Heal;
		See.Heal:
			"####" "#" 0 {
				user_incombat = True;
				crouched = false;
				Speed = Default.Speed;
				bChaseGoal = True;
				bSolid = True;

				A_CheckForPlayer();
			}
			"####" AAAAAA 1 A_WanderGoal(0, 512);
			"####" B 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION);
			"####" BBBBBB 1 A_WanderGoal(0, 512);
			"####" B 0 A_CheckForPlayer();
			"####" CCCCCC 1 A_WanderGoal(0, 512);
			"####" D 0 A_SpawnItemEx("EnemyStep",0,0,14,0,0,0,0,SXF_NOCHECKPOSITION);
			"####" DDDDDD 1 A_WanderGoal(0, 512);
			Loop;
		Heal:
			"####" E 0 {
				crouched = True;
				if (goal) {
					healloopcount = goal.Default.Health > 25 ? min(goal.Default.Health / 25, 3) : 0; // Set the number of Heal iterations to scale with the actor's spawnhealth (max of 3 loops)
				}
			}
		Heal.Loop:
			"####" E 15;
			"####" F 5;
			"####" EEEEE 3 { if (goal) { goal.A_SpawnItemEx("HealingParticle", random(10,-10), random(10,-10), random(16,64), 0, 0, random(1, 2), 0); } } // Spawn healing particles as the actor is ressurected
			"####" E 0 {
				if (healloopcount > 0) {
					healloopcount--;
					return ResolveState("Heal.Loop");
				}

				A_HealGoal();

				return ResolveState("See");
			}
			Goto See;
		Death:
			"####" H 1 {
				if (crouched) { return ResolveState("Death.Crouch"); }
				return ResolveState(null);
			}
			"####" H 4 A_RandomDeathAnim();
			"####" I 5 A_Scream();
			"####" J 5 A_NoBlocking(!user_sneakable);
			"####" KLUV 5;
			"####" W -1;
			Stop;
		Death.Crouch:
			"####" G 5 { crouched = False; }
			"####" Q 5 A_Scream();
			"####" R 5 A_NoBlocking(!user_sneakable);
			"####" STUV 5;
			"####" W -1;
			Stop;
		Death.Front:
			NMDA I 0 A_CheckFadeDeath();
			NMDA I 5 A_Scream();
			"####" J 5 A_UnblockAndDrop();
			"####" K 10;
			"####" L 5;
			"####" M 5;
			"####" N 5;
			"####" O -1;
			Stop;
	}

	state A_CheckForPlayer(bool jump = False)
	{
		Actor p = FindClosestPlayer(self, dist:int(512 + radius));

		// Run away from any player that's close by
		if (p)
		{
			if (Random(0, 255) < 16 || jump) { A_StartSound("Nazi1/Sighted", CHAN_ITEM); }
			Speed = Default.Speed * 2;

			if (Distance3D(p) < 512 + radius)
			{
				target = p;
				body = null;
				goal = null;
				frighttimeout = 35 + Random(0, 14) * 5;
			}

			if (jump && !InStateSequence(CurState, SeeState)) { return ResolveState("See"); }
		} else if (goal) { target = goal; }

		return ResolveState(null);
	}
}