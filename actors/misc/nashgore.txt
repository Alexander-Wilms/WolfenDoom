//const int NASHGORE_BLOODSPOT_DURATION = 1050; //not needed anymore because of cvars - Ozy81
const int NASHGORE_BLOODFLAGS1 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE | SXF_ABSOLUTEVELOCITY;
const int NASHGORE_BLOODFLAGS2 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY | SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION;
const int NASHGORE_BLOODFLAGS3 = SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEPOSITION | SXF_ABSOLUTEANGLE;

// Base blood
ACTOR NashGore_BloodBase : ParticleBase
{
	Game Doom
	-SOLID
	+CANNOTPUSH
	+CORPSE
	+DONTSPLASH
	+FORCEXYBILLBOARD
	+NOBLOCKMAP
	+NOBLOCKMONST
	+NOTELEPORT
}

// Emulated regular Doom blood
ACTOR NashGore_DoomBlood : Blood
{
	+DONTSPLASH
	+FORCEXYBILLBOARD
}

// added for BoA
ACTOR NashGore_RavenBlood : BloodSplatter
{
	+DONTSPLASH
	+FORCEXYBILLBOARD
}

// Modify the internal Blood actor to spawn flying blood
ACTOR NashGore_Blood : Blood replaces Blood
{
	-RANDOMIZE
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItem("NashGore_DoomBlood", 0, 0, 0, 1)
	SpawnDone:
		TNT1 A 0 A_SpawnItemEx("NashGore_FlyingBlood", 0, 0, 0, frandom(0.1, 6.0) * RandomPick(-1, 1), frandom(0.1, 6.0) * RandomPick(-1, 1), frandom(0.0, 6.0), 0, NASHGORE_BLOODFLAGS1, 64)
		TNT1 AA 0 A_SpawnItemEx("NashGore_SplatMaker", 0, 0, 0, frandom(-5, 5), frandom(-5, 5), frandom(0, 5), random(0, 360), NASHGORE_BLOODFLAGS2)
		TNT1 AA 4 A_SpawnItemEx("NashGore_BloodSplasher", 0, 0, 0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(1.0, 2.0), random(0, 360), NASHGORE_BLOODFLAGS1, 128)
		Stop
	}
}

// added for BoA
ACTOR NashGore_BloodSplatter : NashGore_Blood replaces BloodSplatter
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItem("NashGore_RavenBlood", 0, 0, 0, 1)
		TNT1 A 0 A_Jump(256, "SpawnDone")
		Stop
	}
}

// Blood actor that flies outwards and leaves trails behind
ACTOR NashGore_FlyingBlood : NashGore_BloodBase
{
	Radius 8
	Height 2
	Gravity 0.25
	States
	{
	Spawn:
	FlyingNormal:
		TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1)
		TNT1 A 0 A_Jump(12, "RandomlyDestroy")
		TNT1 A 0 A_Jump(27, "FlyingDecel")
		Loop
	FlyingDecel:
		TNT1 A 2 A_SpawnItem("NashGore_FlyingBloodTrail", 0, 0, 0, 1)
		TNT1 A 0 A_ChangeVelocity(velx * 0.8, vely * 0.8, velz, CVF_REPLACE)
		Loop
	RandomlyDestroy:
		TNT1 A 0
		Stop
	Crash:
		TNT1 A 0 A_StartSound("bloodsplat")
		TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
	BloodSplasher:
		TNT1 A 0 A_SpawnItemEx("NashGore_BloodSpot", 0, 0, 0, 0, 0, 0, frandom(0, 360), 7, 128)
		TNT1 AAAAA 1 A_SpawnItemEx("NashGore_BloodSplasher", 0, 0, 0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(1.0, 2.0), random(0, 360), NASHGORE_BLOODFLAGS1, 150)
		Stop
	}
}

// Blood trails spawned by FlyingBlood
ACTOR NashGore_FlyingBloodTrail : NashGore_BloodBase
{
	Radius 1
	Height 1
	Gravity 0.15
	Scale 0.55
	States
	{
	Spawn:
		BTRL ABCD 4
		Loop
	Crash:
		TNT1 A 0
		Stop
	}
}

// Blood splashers, generates Random splashes
ACTOR NashGore_BloodSplasher : NashGore_BloodBase
{
	Radius 1
	Height 1
	Gravity 0.20
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Crash:
		TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
		TNT1 A 0 A_SpawnItemEx("NashGore_BloodSpot", 0, 0, 0, 0, 0, 0, random(0, 360), NASHGORE_BLOODFLAGS3, 200)
		Stop
	}
}

// Blood spots that temporarily stay on the floor
ACTOR NashGore_BloodSpot : NashGore_BloodBase
{
	Radius 4
	Height 2
	Alpha 0.85
	-NOBLOCKMAP
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("NashGore_BloodSplash", 0, 0, 0, 1)
		TNT1 A 0 A_Jump(256, "BloodSpot1", "BloodSpot2", "BloodSpot3", "BloodSpot4")
	BloodSpot1:
		BSPT A 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	BloodSpot2:
		BSPT B 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	BloodSpot3:
		BSPT C 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	BloodSpot4:
		BSPT D 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	FadeOut:
		"####" "#" 2 A_FadeOut(0.005)
		Loop
	}
}

// Blood splashes
ACTOR NashGore_BloodSplash : NashGore_BloodBase
{
	Radius 1
	Height 2
	Scale 0.5
	-NOBLOCKMAP
	States
	{
	Spawn:
		BSPL ABCDE 5
		Stop
	}
}

// Blood splat decal maker
ACTOR NashGore_SplatMaker
{
	Radius 4
	Height 8
	PROJECTILE
	States
	{
	Spawn:
		TNT1 A 3
	Death:
		TNT1 A 1
		Stop
	}
}

// BOA Translations
ACTOR BossBlood : NashGore_Blood {Translation "16:47=112:127","168:191=112:127"}
ACTOR BossFlyingBlood : NashGore_FlyingBlood {Translation "16:47=112:127","168:191=112:127"}
ACTOR MutantBlood : NashGore_Blood {Translation "16:47=[205,0,215]:[40,0,96]","168:191=[205,0,215]:[40,0,96]"}
ACTOR MutantFlyingBlood : NashGore_FlyingBlood {Translation "16:47=[205,0,215]:[40,0,96]","168:191=[205,0,215]:[40,0,96]"}
ACTOR ScorpionBlood : NashGore_Blood {Translation "16:47=208:223","168:191=210:224"}
ACTOR ScorpionFlyingBlood : NashGore_FlyingBlood {Translation "16:47=208:223","168:191=210:224"}
ACTOR UndeadBlood : NashGore_Blood {Translation "16:47=[34,0,7]:[0,0,0]","168:191=[36,0,9]:[2,0,0]"}
ACTOR UndeadFlyingBlood : NashGore_FlyingBlood {Translation "16:47=[34,0,7]:[0,0,0]","168:191=[36,0,9]:[2,0,0]"}
ACTOR UndeadFlyingBloodTrail : NashGore_FlyingBloodTrail {Translation "16:47=[34,0,7]:[0,0,0]","168:191=[36,0,9]:[2,0,0]"}

ACTOR Pain_Overlay
{
	Radius 1
	Height 1
	-SOLID
	+DONTSPLASH
	+NOBLOCKMAP
	+NOGRAVITY
	+NOINTERACTION
	+NOTELEPORT
	States
	{
	Spawn:
		PNSP A 1
		"####" A 0 A_Fadeout(.1)
		Loop
	}
}

//Zombie related variants - needed for brightmaps
ACTOR Zombie_DoomBlood : NashGore_DoomBlood
{
	Decal "ZBloodSmear"
	Translation "16:47=[0,112,69]:[0,112,85]","168:191=[0,112,69]:[0,112,85]"
}

ACTOR ZombieBlood : NashGore_Blood
{
	Decal "ZBloodSmear"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItem("Zombie_DoomBlood", 0, 0, 0, 1)
	SpawnDone:
		TNT1 A 0 A_SpawnItemEx("Zombie_FlyingBlood", 0, 0, 0, frandom(0.1, 6.0) * RandomPick(-1, 1), frandom(0.1, 6.0) * RandomPick(-1, 1), frandom(0.0, 6.0), 0, NASHGORE_BLOODFLAGS1, 64)
		TNT1 AA 0 A_SpawnItemEx("Zombie_SplatMaker", 0, 0, 0, frandom(-5, 5), frandom(-5, 5), frandom(0, 5), random(0, 360), NASHGORE_BLOODFLAGS2)
		TNT1 AA 4 A_SpawnItemEx("Zombie_BloodSplasher", 0, 0, 0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(1.0, 2.0), random(0, 360), NASHGORE_BLOODFLAGS1, 128)
		Stop
	}
}

ACTOR Zombie_BloodSplatter : NashGore_BloodSplatter
{
	Decal "ZBloodSmear"
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_SpawnItem("NashGore_RavenBlood", 0, 0, 0, 1)
		TNT1 A 0 A_Jump(256, "SpawnDone")
		Stop
	}
}

ACTOR Zombie_FlyingBlood : NashGore_FlyingBlood
{
	Decal "ZBloodSmear"
	States
	{
	Spawn:
	FlyingNormal:
		TNT1 A 2 A_SpawnItem("Zombie_FlyingBloodTrail", 0, 0, 0, 1)
		TNT1 A 0 A_Jump(12, "RandomlyDestroy")
		TNT1 A 0 A_Jump(27, "FlyingDecel")
		Loop
	FlyingDecel:
		TNT1 A 2 A_SpawnItem("Zombie_FlyingBloodTrail", 0, 0, 0, 1)
		TNT1 A 0 A_ChangeVelocity(velx * 0.8, vely * 0.8, velz, CVF_REPLACE)
		Loop
	RandomlyDestroy:
		TNT1 A 0
		Stop
	Crash:
		TNT1 A 0 A_StartSound("bloodsplat")
		TNT1 A 0 A_SpawnItem("Zombie_BloodSplash", 0, 0, 0, 1)
	BloodSplasher:
		TNT1 A 0 A_SpawnItemEx("Zombie_BloodSpot", 0, 0, 0, 0, 0, 0, frandom(0, 360), 7, 128)
		TNT1 AAAAA 1 A_SpawnItemEx("Zombie_BloodSplasher", 0, 0, 0, frandom(-2.0, 2.0), frandom(-2.0, 2.0), frandom(1.0, 2.0), random(0, 360), NASHGORE_BLOODFLAGS1, 150)
		Stop
	}
}

ACTOR Zombie_FlyingBloodTrail : NashGore_FlyingBloodTrail
{
	Decal "ZBloodSmear"
	States
	{
	Spawn:
		ZTRL ABCD 4
		Loop
	}
}

ACTOR Zombie_BloodSplasher : NashGore_BloodSplasher
{
	Decal "ZBloodSmear"
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Crash:
		TNT1 A 0 A_SpawnItem("Zombie_BloodSplash", 0, 0, 0, 1)
		TNT1 A 0 A_SpawnItemEx("Zombie_BloodSpot", 0, 0, 0, 0, 0, 0, random(0, 360), NASHGORE_BLOODFLAGS3, 200)
		Stop
	}
}

ACTOR Zombie_BloodSpot : NashGore_BloodSpot
{
	Decal "ZBloodSmear"
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem("Zombie_BloodSplash", 0, 0, 0, 1)
		TNT1 A 0 A_Jump(256, "BloodSpot1", "BloodSpot2", "BloodSpot3", "BloodSpot4")
	BloodSpot1:
		ZSPT AEI 1 A_SetTics(random(24,35))
		"####" M 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	BloodSpot2:
		ZSPT BFJ 1 A_SetTics(random(24,35))
		"####" N 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	BloodSpot3:
		ZSPT CGK 1 A_SetTics(random(24,35))
		"####" O 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	BloodSpot4:
		ZSPT DHL 1 A_SetTics(random(24,35))
		"####" P 0 A_SetTics(35*CallACS("BloodLifetime"))
		"####" "#" 0 A_Jump(256, "FadeOut")
	FadeOut:
		"####" "#" 2 A_FadeOut(0.005)
		Loop
	}
}

ACTOR Zombie_BloodSplash : NashGore_BloodSplash
{
	Decal "ZBloodSmear"
	States
	{
	Spawn:
		ZSPL ABCDE 5
		Stop
	}
}

ACTOR Zombie_SplatMaker : NashGore_SplatMaker
{
	Decal "ZBloodSmear"
	Translation "16:47=[0,112,69]:[0,112,85]","168:191=[0,112,69]:[0,112,85]"
}

ACTOR ZPain_Overlay : Pain_Overlay
{
	States
	{
	Spawn:
		PNSZ A 1
		"####" A 0 A_Fadeout(.1)
		Loop
	}
}