ACTOR HQ_Checker : Inventory {Inventory.MaxAmount 1}

ACTOR GrenadePickup : CustomInventory
{
	//$Category Weapons (BoA)
	//$Title Grenade
	//$Color 14
	Scale 0.25
	Inventory.PickupMessage "$GRENADE"
	Tag "$TAGFRAGN"
	Inventory.Icon THRPB0
	Inventory.MaxAmount 9
	Inventory.InterHubamount 9
	Inventory.UseSound "grenade/throw"
	Inventory.PickupSound "grenade/pickup"
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		THRP A -1
		Stop
	Use:
		TNT1 A 0 A_JumpIfInventory("HQ_Checker",1,"Stupid")
		TNT1 A 0 A_JumpIfInventory("AstroGrenadeToken", 1, 2)
		TNT1 A 0 A_FireProjectile("HandGrenade",0,0,0,0,FPF_NOAUTOAIM)
		Stop
		TNT1 A 0 {
			A_TakeInventory("AstroGrenadeToken",1,TIF_NOTAKEINFINITE);
			A_FireProjectile("AstroHandGrenade",0,0,0,0,FPF_NOAUTOAIM);
		}
		Stop
	Stupid:
		TNT1 A 0 A_Print("$STUPID1",3.5)
		Fail
	}
}

ACTOR HandGrenade : GrenadeBase
{
	Radius 4
	Height 3
	Speed 40
	FastSpeed 45
	Damage (1)
	DamageType "Frag"
	Scale 0.25
	Projectile
	-NOGRAVITY
	-NOTELEPORT
	+WINDTHRUST
	+FORCEXYBILLBOARD
	Obituary "$OBGRENADE"
	BounceType Doom
	BounceFactor 0.2
	WallBounceFactor 0.6
	BounceSound "grenade/bounce"
	GrenadeBase.FearDistance 192
	GrenadeBase.SplashType "Missile"
	States
	{
	Spawn:
		THRW A 2 A_CountDown
		THRW BCDEFGH 2
		TNT1 A 0 A_JumpIf(waterlevel == 3, "AdjustMass")
		Loop
	AdjustMass:
		"####" "#" 0 { A_SetMass(800); ClearBounce; }
		Goto Swim
	Swim:
		THRW A 2 { A_CountDown; A_ScaleVelocity(0.7); A_SpawnItemEx("Bubble", 0, 0, 0, 0, 0, 2, random (0, 360), 0, 128); }
		THRW BCDEFGH 2
		Loop
	Death:
		THRW B 35 { floorclip -= 2; }
		TNT1 A 0 A_SpawnGroundSplash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_StartSound("grenade/explode", CHAN_AUTO, 0, 1.0, 0.1)
		TNT1 A 1 A_SpawnItemEx("GeneralExplosion_Medium")
		TNT1 A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}

ACTOR HansGrenade : HandGrenade //Fast version for Afrika Boss
{
	Speed 50
	FastSpeed 55
	BounceFactor 0.4
	WallBounceFactor 0.8
	States
	{
	Death:
		THRW B 10 { floorclip -= 2; }
		TNT1 A 0 A_SpawnGroundSplash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_StartSound("grenade/explode", CHAN_AUTO, 0, 1.0, 0.1)
		TNT1 A 1 A_SpawnItemEx("GeneralExplosion_Medium")
		TNT1 A 1 Radius_Quake(20,20,0,32,0)
		Stop
	}
}

ACTOR VeryFastGrenade : HandGrenade //Very fast version for Grenadeers
{
	Speed 100
	FastSpeed 105
	BounceFactor 0.4
	WallBounceFactor 0.8
	States
	{
	Death:
		THRW B 35 { floorclip -= 2; }
		TNT1 A 0 A_SpawnGroundSplash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_StartSound("grenade/explode", CHAN_AUTO, 0, 1.0, 0.1)
		TNT1 A 1 A_SpawnItemEx("GeneralExplosion_Medium")
		TNT1 A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}

ACTOR SlowGrenade : HandGrenade //Slow version for Grenadeers
{
	Speed 20
	FastSpeed 25
	BounceFactor 0.5
	WallBounceFactor 1.0
	States
	{
	Death:
		THRW B 45 { floorclip -= 2; }
		TNT1 A 0 A_SpawnGroundSplash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_StartSound("grenade/explode", CHAN_AUTO, 0, 1.0, 0.1)
		TNT1 A 1 A_SpawnItemEx("GeneralExplosion_Medium")
		TNT1 A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}

ACTOR VerySlowGrenade : HandGrenade //Very slow version for Grenadeers
{
	Speed 10
	FastSpeed 15
	BounceFactor 0.8
	WallBounceFactor 1.6
	States
	{
	Death:
		THRW B 70 { floorclip -= 2; }
		TNT1 A 0 A_SpawnGroundSplash
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SetScale(1.75,1.75)
		TNT1 A 0 A_SetTranslucent(0.75,1)
		TNT1 A 0 A_StartSound("grenade/explode", CHAN_AUTO, 0, 1.0, 0.1)
		TNT1 A 1 A_SpawnItemEx("GeneralExplosion_Medium")
		TNT1 A 1 Radius_Quake(10,10,0,16,0)
		Stop
	}
}

// Given to players when leaving INTERMAP. Restores the player's ability to
// throw grenades returns non-training grenades to them, and clears their
// inventory (in case they were in training area when map changed).
// Added also MineSweeper removal for playism purposes - Ozy81
ACTOR PlayerLeavingHQ : CustomInventory
{
	Inventory.MaxAmount 0
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Pickup:
		TNT1 A 0 {
			// Return seized grenades to player
			if (CountInv("HQ_Checker") == 0) {
				A_TakeInventory("GrenadePickup", 0x7FFFFFFF);
				A_TakeInventory("MineSweeper", 0x7FFFFFFF);
				A_TakeInventory("Power", 0x7FFFFFFF);
				A_TakeInventory("Sten", 0x7FFFFFFF);
				A_TakeInventory("9mmAmmo", 0x7FFFFFFF);
				A_GiveInventory("GrenadePickup", CountInv("NonTrainingGrenade"));
				A_GiveInventory("MineSweeper", CountInv("NonTrainingMineSweeper"));
				A_GiveInventory("Power", CountInv("NonTrainingPower"));
				A_GiveInventory("Sten", CountInv("NonTrainingSten"));
				A_GiveInventory("9mmAmmo", CountInv("NonTraining9mmAmmo"));
				A_TakeInventory("NonTrainingGrenade", 0x7FFFFFFF);
				A_TakeInventory("NonTrainingMineSweeper", 0x7FFFFFFF);
				A_TakeInventory("NonTrainingPower", 0x7FFFFFFF);
				A_TakeInventory("NonTrainingSten", 0x7FFFFFFF);
				A_TakeInventory("NonTraining9mmAmmo", 0x7FFFFFFF);
			}

			// Restore player's ability to throw grenades & to use minesweeper
			A_TakeInventory("HQ_Checker", 1);

			// Clear the player's inventory (in case they're in training area)
			ACS_NamedExecuteWithResult("CLEAR INVENTORY", 0);
		}
		Stop
	}
}

// Counts the grenades that were seized from the player when they entered the
// training area. This provides support for any number of players.
ACTOR NonTrainingGrenade : Inventory
{
	Inventory.MaxAmount 9
	+INVENTORY.UNDROPPABLE
	+INVENTORY.UNTOSSABLE
}

ACTOR NonTrainingMineSweeper : NonTrainingGrenade { Inventory.MaxAmount 1 }
ACTOR NonTrainingSten : NonTrainingGrenade { Inventory.MaxAmount 1 }
ACTOR NonTraining9mmAmmo : NonTrainingGrenade { Inventory.MaxAmount 256 }
ACTOR NonTrainingPower : NonTrainingGrenade { Inventory.MaxAmount 2400 }