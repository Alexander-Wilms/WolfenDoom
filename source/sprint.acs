//sprinting stamina
int cooldown[MAX_PLAYERS];
int staminarecoverytimeout[MAX_PLAYERS];

function void DoSprinting(void)
{
	if (CheckInventory("IsDragging")) { return; }

	bool checksprint = GetCVar("boa_sprintswitch");
	int buttons = GetPlayerInput(-1, INPUT_BUTTONS);
	int p = PlayerNumber();

	int speed = 1.0;

	// Reset to full stamina if noclipping, and increase base speed
	if (ScriptCall("ACSTools", "IsNoClipping"))
	{
		speed = 2.5;
		cooldown[p] = 0;
		staminarecoverytimeout[p] = 0;
		GiveInventory("Stamina", 100);
	}

	if (
		(buttons & BT_SPEED) && 
		(buttons & BT_FORWARD) && 
		!(buttons & BT_CROUCH) && //you can still sprint with 'toggled' crouching!
		cooldown[p] <= 0 &&
		CheckInventory("Stamina") && 
		!ScriptCall("ACSTools", "IsHidden") // Don't allow sprinting if you are wearing a NOTARGET disguise unless you are not currently hidden
	)
	{
		speed *= 2;

		// Don't use stamina if stamina check is disabled or if Totale Macht/Berserk is active
		if (!checksprint && !CheckInventory("BerserkToken"))
		{
			TakeInventory("Stamina", Random(1, 3)); //more stamina to use
		}
	}
	else
	{
		if (staminarecoverytimeout[p] < Timer())
		{
			cooldown[p]--;
			GiveInventory("Stamina",1);
			staminarecoverytimeout[p] = Timer() + Random(1, 3); //recovery is more fast
		}
	}

	SetActorProperty(0, APROP_SPEED, speed);

	if (!CheckInventory("Stamina")) { cooldown[p] = 100; }
}

int staminasoundtimeout[MAX_PLAYERS];

function void DoStaminaCheck(void)
{
	int p = PlayerNumber();

	if(CheckInventory("Stamina")<25 && staminasoundtimeout[p] < Timer())
	{
		if(GetActorProperty(0,APROP_WATERLEVEL)<3)
		PlaySound(0,"player/breathing",CHAN_VOICE);
		staminasoundtimeout[p] = Timer() + 50;
	}
}