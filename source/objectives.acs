int objectives_on[MAX_PLAYERS];			//Objective display toggle

int objectives_active = 0;
str objectives[MAX_OBJECTIVES][2]=
{
	{"",""},
	{"",""},
	{"",""},
	{"",""},
	{"",""},
	{"",""}
};

script "boaobjectives" (void)
{
	If(
		(GetLevelInfo(LEVELINFO_LEVELNUM)==99)
	)
	Terminate;
	
	If(
		(CheckActorClass(PlayerNumber(),"KeenPlayer"))
	)
	Terminate;

	if(objectives_on[PlayerNumber()])
		objectives_on[PlayerNumber()]=0;
	else
		objectives_on[PlayerNumber()]=1;
}

/* Change an objective
 * mode - 0 for icon, 1 for text.
 * objnumber - 0-indexed objective number. 0-2 are primary, 3-5 are secondary.
 * objtext - Objective text. MO_ICON_OPN or MO_ICON_ACC if mode is 0, LANGUAGE entry name if mode is 1.
 */
script "boaobjectivesset"(int mode,int objnumber,int objtext)
{
	if (mode == 1) { // Objective text
		// No curly braces needed here, since there's just one statement after the if/else.
		if (StrLen(StrParam(l:objtext)) > 0)
			objectives_active |= (1 << objnumber);
		else
			// Blank objective text
			objectives_active &= ~(1 << objnumber);
	}
	objectives[objnumber][mode]=objtext;
}

function void DrawObjectives(void)
{
	if (!objectives_on[PlayerNumber()]) { return; }

	sethudsize(800,600,1);
	setfont("SMALLFONT");

	int intMsgID = 960;

	int intHeaderCount = 0;

	for (int i = 0; i <= 7; i++)
	{
		SetFont("SMALLFONT");
		int intPosY = 135.0 + (13.0 * (intMsgID - 960)) + 0.1;

		if (i == 0)
		{
			HudMessage(l:"MO_PRIMARY"; HUDMSG_PLAIN, intMsgID++, CR_UNTRANSLATED, 300.1, intPosY, 0.03);
			intHeaderCount++;
		}
		else if (i == 4)
		{
			HudMessage(l:"MO_SECONDARY"; HUDMSG_PLAIN, intMsgID++, CR_UNTRANSLATED, 300.1, intPosY, 0.03);
			intHeaderCount++;
		}
		else
		{
			int objIdx = i - intHeaderCount;
			str strOutput = StrParam(l:objectives[objIdx][0], l:objectives[objIdx][1]);

			If (
				strLen(strOutput) > 0 &&
				((objectives_active >> objIdx) & 1) == 1)
			{
				HudMessage(s:strOutput; HUDMSG_PLAIN, intMsgID++, CR_UNTRANSLATED, 310.1, intPosY, 0.03);

				If (!StrCmp(StrParam(l:objectives[objIdx][0]), StrParam(l:"MO_ICON_ACC")))
					SetFont("OBJ_YES");
				Else
					SetFont("OBJ_NO");
				HudMessage(s:"A"; HUDMSG_PLAIN, intMsgID - 10, CR_UNTRANSLATED, 300.1, intPosY + 1.0, 0.03);
			}
		}
	}

	int intPosX = 400.0;
	if (
		StrCmp(StrLeft(GetCvarString("language"), 2), "en") == 0 ||
		StrCmp(GetCvarString("language"), "default") == 0)
	{
		SetFont("OBJECTGX");
	}
	else
	{
		SetFont("OBJECTGY");
		intPosX += 40.0;
	}
	hudmessage(s:"A";
		HUDMSG_PLAIN,972,CR_UNTRANSLATED,intPosX,115.1,0.03);
}

script "boaobjectiveaccomplished" (void)
{
	AmbientSound("misc/objective_acc", 57);

	sethudsize(800,600,1);
	setfont("OBJICON");
	hudmessage(s:"A";
		HUDMSG_FADEINOUT,990,CR_UNTRANSLATED,400.0,115.1,1.0,0.5,0.5);
	setfont("SMALLFONT");
	hudmessage(l:"MO_ACCOMP";
		HUDMSG_FADEINOUT,989,CR_UNTRANSLATED,400.0,130.1,1.0,0.5,0.5);

	ScriptCall("BoACompass", "Flash");
}

script "boaobjectiveadded" (void)
{
	AmbientSound("misc/objective_add", 57);
	sethudsize(800,600,1);
	setfont("OBJADD");
	hudmessage(s:"A";
		HUDMSG_FADEINOUT,990,CR_UNTRANSLATED,400.0,115.1,1.0,0.5,0.5);
	setfont("SMALLFONT");
	hudmessage(l:"MO_ADDED";
		HUDMSG_FADEINOUT,989,CR_UNTRANSLATED,400.0,130.1,1.0,0.5,0.5);

}

script "CompleteObjective" (int num)
{
	ACS_NamedExecuteAlways("boaobjectivesset", 0, 0, num, "MO_ICON_ACC");
}